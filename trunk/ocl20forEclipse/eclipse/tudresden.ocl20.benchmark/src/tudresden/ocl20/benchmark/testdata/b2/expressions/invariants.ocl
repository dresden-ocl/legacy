package tudresden::ocl20::benchmark::testdata::b2

context Person 
  inv attributesDefined:
	 name.isDefined and civstat.isDefined and
      gender.isDefined and alive.isDefined
      
context Person 
  inv nameCapitalThenSmallLetters:
      let small:Set(String)=
        Set{'a','b','c','d','e','f','g','h','i','j','k','l','m',
            'n','o','p','q','r','s','t','u','v','w','x','y','z'} in
      let capital:Set(String)=
        Set{'A','B','C','D','E','F','G','H','I','J','K','L','M',
            'N','O','P','Q','R','S','T','U','V','W','X','Y','Z'} in
      capital->includes(name.substring(1,1)) and
      Set{2..name.size}->forAll(i |
        small->includes(name.substring(i,i)))

context Person 
  inv nameIsUnique:
	 Person.allInstances()->forAll(self2|
      self<>self2 implies self.name<>self2.name)
      
context Person 
  inv femaleHasNoWife:
	 gender=Gender::female implies wife.isUndefined
  
context Person 
  inv maleHasNoHusband:
	 gender=Gender::male implies husband.isUndefined
	
endpackage
  