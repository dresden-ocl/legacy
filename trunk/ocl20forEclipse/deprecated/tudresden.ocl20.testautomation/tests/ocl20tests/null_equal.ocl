-----------------------------
-- This test suite is meant to test 
-- the guts of the special types invalid and undefined
-----------------------------
package uml


context Model
  -- Collection with null
  def: nSet:Set(OclAny)=null

context Model
  -- collection containing null
  def: ncSet:Set(OclAny)=Set{null}

context Model
  -- oclAny with null
  def: nAny:OclAny=null
  
context Model
  -- null Integer
  def: nInt:Integer=null
  
context Model
  -- null Real
  def: nReal:Real=null
  
context Model
  -- null instance to myself
  def: nInst:Model =null


-- Equality of Invalid and 
-- Undefined like the table in the manual, P.15


-- EQUALITY

-- pure and simple
context Model inv eq2null_doubleNull:
  null = null
  
-- both are OclAny
context Model inv eq2null_any1:
    self.nAny = self.nAny
    
context Model inv eq2null_any2:
    self.nAny = null and null = self.nAny and self.nAny.oclIsUndefined()
    
context Model inv eq2null_prim1:
  self.nInt = self.nReal and self.nReal = self.nInt
    
context Model inv eq2null_prim2:
  self.nInt = null and null = self.nInt 
  and self.nReal = null and null = self.nReal 
  and self.nReal.oclIsUndefined() and self.nInt.oclIsUndefined()
    
-- compare oclAny and a primitive (real in this case, doesn't matter which one exactly)
context Model inv eq2null_primAny1:
  self.nReal = self.nAny and self.nAny = self.nReal
  and self.nReal.oclIsUndefined() and self.nInt.oclIsUndefined()
  
-- collections
-- this fails. Check if that is correct. If not, feel free to delete :)
context Model inv eq2null_coll1:
  self.nSet = null and null = self.nSet
  and self.nSet = self.nSet
  and self.nSet.oclIsUndefined()

-- collections and OclAny
-- this fails. Check if that is correct. If not, feel free to delete :)  
context Model inv eq2null_collAny:
  self.nSet = self.nAny and self.nAny = self.nSet
  
-- collections and primitives
-- this fails. Check if that is correct. If not, feel free to delete :)
context Model inv eq2null_collPrim:
  self.nSet = self.nReal and self.nReal = self.nSet
  
-- model instance
context Model inv eq2null_inst:
  self.nInst = null and null = self.nInst
  and self.nInst.oclIsUndefined()
  
-- model instance and oclAny
context Model inv eq2null_instAny:
  self.nInst = self.nAny and self.nAny = self.nInst
  
-- model instance and Primitive
context Model inv eq2null_instPrim:
  self.nInst = self.nReal and self.nReal = self.nInst
  
  
-- model instance and Collection
context Model inv eq2null_instColl:
  self.nInst = self.nSet and self.nSet = self.nInst
  

-- null-containing-collection
-- does this test make sense?
context Model inv eq2null_nullCollection:
  self.ncSet = self.ncSet
  
endpackage