-- @model{../model/pml.ecore}
package tudresden::ocl20::pivot::examples::pml

-- The id of a plug-in must be defined.
context Plugin
inv pluginIdIsDefined: 
  not self.id.oclIsUndefined()

-- The name of a plug-in must be defined.
context Plugin
inv pluginNameIsDefined:
  not self.name.oclIsUndefined()

-- Each extension point of a plug-in must have a unique name.
context Plugin
inv pluginHasUniqueFeatureId:
  self.extensionPoints->isUnique(id)


-- The id of a feature must be defined.
context Feature
inv featureIdIsDefined: 
  not self.id.oclIsUndefined()

-- The name of a feature must be defined.
context Feature
inv featureNameIsDefined:
  not self.name.oclIsUndefined()

-- Each feature must have at least one plug-in.
context Feature
inv featureContainsPlugins:
  self.plugins->size() > 0
  
-- Each feature must have the same version as all contained plug-ins.
context Feature
inv featureAndPluginsHaveSameVersion:
  self.version.oclIsUndefined()
  or
  self.plugins->forAll(p : Plugin | p.version = self.version)
  

-- The id of an extension point must be defined.
context ExtensionPoint 
inv extensionPointIdIsDefined:
  not self.id.oclIsUndefined()

-- An extension point must provide an interface.
context ExtensionPoint 
inv extensionPointInterfacesAreSet:
  not self.interface.oclIsUndefined()


-- The name of a java type must be defined.
context JavaType
inv javaTypeNameIsDefined:
  not self.fullyQualifiedName.oclIsUndefined()


endpackage